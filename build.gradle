plugins {
	id "dev.architectury.loom" version "1.2-SNAPSHOT"
	id "io.github.juuxel.loom-quiltflower" version "1.7.2"
	id "maven-publish"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '17'

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: '9999'}"
archivesBaseName = "${project.archives_base_name}-fabric"
group = project.maven_group

loom {
	silentMojangMappingsLicense()
}

repositories {
	maven {
		url "https://maven.saps.dev/releases"
	}

	// the maven for the devOS repo, for create fabric
	// (and a bunch of libraries I'm assuming)
	maven { url "https://mvn.devos.one/snapshots/" }
	maven { url "https://mvn.devos.one/releases/" }

	maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes

	maven {
		url "https://maven.tterrag.com/"
		content {
			includeGroup "com.jozufozu.flywheel"
		}
	}

	// jitpack?! why?!
	maven {
		url "https://jitpack.io/"
		content {
			includeGroup "com.github.LlamaLad7"
			includeGroup "com.github.Chocohead"
			includeGroup "com.github.Draylar.omega-config"
		}
	}

	// "i play both sides, that way i always come out ahead"
	maven {
		url "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings loom.officialMojangMappings()

	modApi("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
	modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

	modApi "dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}"
	modApi "com.simibubi.create:create-fabric-${rootProject.minecraft_version}:${rootProject.create_version}"

	modLocalRuntime modCompileOnly("curse.maven:jei-238222:4434392")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

jar {
	archiveClassifier.set "dev"
}

remapJar {
	archiveBaseName.set "${rootProject.archives_base_name}-fabric"
	archiveClassifier.set null
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = project.curseforge_type
			addGameVersion "Fabric"
			addGameVersion "1.18.2"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'kubejs'
				requiredDependency 'create-fabric'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.archivesBaseName
			from components.java
		}
	}

	repositories {
		if (ENV.FTB_MAVEN_TOKEN) {
			maven {
				url "https://maven.ftb.dev/release"
				credentials {
					username = "latvian"
					password = "${ENV.FTB_MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

